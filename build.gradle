defaultTasks "dist"

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
            project.android {
                lintOptions {
                    disable "InvalidPackage", "IconColors", "OldTargetApi"
                    checkReleaseBuilds true
                }
                packagingOptions {
                  exclude "META-INF/LICENSE"
                  exclude "META-INF/NOTICE"
                }
            }
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task distCopyApk(type: Copy) {
    dependsOn "mobile:lint", "mobile:assembleRelease"
    from 'mobile/build/outputs/apk/mobile-release.apk'
    into 'build/apk'
    rename "mobile-release.apk", "DSAndroid.apk"
}

task dist(description: "Builds DSAndroid", group: "build") {
    dependsOn "distCopyApk"
}
