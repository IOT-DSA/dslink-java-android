defaultTasks "dist"

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    tasks.withType(JavaCompile) {
        options.incremental = true
    }

    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
            project.android {
                lintOptions {
                    disable "InvalidPackage", "IconColors", "OldTargetApi"
                    checkReleaseBuilds true
                }
            }
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task distCopyApk(type: Copy) {
    dependsOn "mobile:lint", "mobile:assembleRelease"
    from 'mobile/build/outputs/apk/mobile-release.apk'
    into 'build/apk'
    rename "mobile-release.apk", "DGMobile.apk"
}

task dist(description: "Builds DGMobile", group: "build") {
    dependsOn "distCopyApk"
}
